:import adventure_lib
:name {package}:init

:budget_cap 201
; minimum budget cap so that init can quit in 1 frame

:const string hide_block "adv---Stop hiding---"
#hide_blocker global.string.get(hide_block)

:global double leon.adventure.maxDifficulty
:global string leon.adventure.path

:local double maxDifficultyInit
:local double difficulty
:local double inc
:local double pos
; 
:local string market_data
:local string farm_dir
:local string dir
; 
:local bool square_up

:local bool parity
:local bool needs_something
:local bool skipped

key.{reinit}()
; reinitialize maxDifficulty
; 
key.{start}()
; Toggle the script state
; 
key.{up}()
key.{down}()
; Increment/Decrement maxDifficulty by 1
; 
key.{kill}()
; Shut down the AI and enters farm keys to
; top us off, then, it terminates the program

isopen("arcade")

; Square pattern
; RULDRU
; pos = if(\
  x(adventure.playerCoords()) == if(pos % 4 >= 2, 127 - i, 127 + i) &&\
  y(adventure.playerCoords()) == if(\
    pos > 2, \
    min(127 + i, 127 + i * (pos % 5)), \
    max(127 - i, 127 - i * (pos % 5))\
  ), \
  (pos + 1) % 6, \
  pos\
)
; direction = sub("RULD", pos % 4, 1)

; diamond pattern
; Player spawns in the middle of the map at 127.0, 127.0
; going R = x + 1.0
; going U = y + 1.0
; going L = x - 1.0
; going D = y - 1.0
#nextRoomLower if(\
  x(adventure.roomCoords()) < 127.0 && \
  y(adventure.roomCoords()) >= 127.0,\
  "R",\
  if(\
    x(adventure.roomCoords()) >= 127.0 && \
    y(adventure.roomCoords()) > 127.0,\
    "D",\
    if(\
      x(adventure.roomCoords()) > 127.0 && \
      y(adventure.roomCoords()) <= 127.0,\
      "L",\
      "U" \
    )\
  )\
)
#rotateLeft(x) sub("LDRU", index("ULDR", {x}, 0), 1)
; Conditionally rotate the direction "x" to the left.
; The benefit of this macro is that it only expands "x" once.
#condRotateLeft(cond, x) sub(\
  if({cond}, "LDRU", "ULDR"), \
  index("ULDR", {x}, 0), \
  1\
)

#count.entities(entity) adventure.countEntities("{entity}")
; helper macro to shorten the process for counting entities

; ======== Start init code ==============

; This exists as a local variable solely to provide users
; an easy place to customize it, after it's been imported.
maxDifficultyInit = 81.0

; can replace previous init with this to imitate a full completion
;maxDifficultyInit = {cur_difficulty(1.0)}

; Initialize all our global variables.
; Make sure that maxDifficulty is at least 2.
; if it's 1 it gets stuck in an endless loop
; in the starting room for some reason
leon.adventure.maxDifficulty = if(\
  leon.adventure.maxDifficulty < 2.0 || impulse() == "key.{reinit}",\
  max(2.0, maxDifficultyInit),\
  if(\
    contains("key.{start}|key.{kill}", impulse()),\
    leon.adventure.maxDifficulty,\
    if(\
      impulse() == "key.{up}", \
      leon.adventure.maxDifficulty + 1.0,\
      max(2.0, leon.adventure.maxDifficulty - 1.0)\
    )\
  )\
)

; This variable also begins the variable hiding block with its name.
{target.setPos({target.getPositions})}

; 
; Change the state of the script.
; If we use key.{kill}, our state must be off, otherwise, we
; try to toggle our state.
{state.set(\
  impulse() != "key.{kill}" && \
  {state.get} != (impulse() == "key.{start}")\
)}

leon.adventure.path = leon.adventure.path
global.string.set(hide_block, "</size>")

goto(if(\
  impulse() == "key.{kill}" || \
  impulse() == "key.{start}" && {state.get}, \
  start, \
  end\
))

; ======== Continuous loop code ==============


#can_leech adventure.hasItem("leechSword")
#can_cast adventure.hasItem("bookSpells")
#has_reaver adventure.hasItem("manaReaver")

#needs_bombs adventure.bombs() < 99 && difficulty >= 80.0 && {can_leech}
#needs_health adventure.playerHealth() < 99 && {can_leech}
#needs_mana adventure.mana() < 99 && {has_reaver} && {can_cast}
#needs_keys adventure.keys() <= 10

#any_needs {needs_bombs} || {needs_health} || {needs_mana} || {needs_keys}

#num_enemies round(difficulty * 0.1 + 3.0)

; calculate how much health an elite has
#elite_health d2i(floor(difficulty * 0.01 + 1.0))
; 
; calculate how much damage an elite deals
#elite_dmg d2i(ceil(ceil(difficulty * 0.39) * 1.5))
; 
; calculate how much damage an elite will actually deal to us
#elite_trueDMG {elite_health} * ({elite_dmg} - adventure.playerArmor())

next:
; partity is in charge of making sure we never exit farm keys until
; we're back in the room we started in
parity = not(parity)
adventure.move({nextDir(farm_dir)})

start:
difficulty = {cur_difficulty(0.0)}
needs_something = {survivable(difficulty)} && ({any_needs})
; "XU   L   R   D"   = "X{dir_spaced(3)}"
; " 918 09  189 09"
:const string exit_coord " 918 09  189 09"
#exit_concat x(adventure.playerPos()) . y(adventure.playerPos())
farm_dir = sub("X{dir_spaced(3)}", index(exit_coord, {exit_concat}, 0), 1)
; get our exit direction

skipped = contains("ULRD", farm_dir) && (\
  (adventure.countEntities("Elite") > 0) != \
  (needs_something && {needs_bombs}) && (\
    adventure.countEntities("Elite") > 0 || \
    adventure.playerHealth() > 100 - d2i({num_enemies} * 1.8)\
  ) || (\
    adventure.countEntities("Elite") > 0 && \
    adventure.playerHealth() <= {elite_trueDMG}\
  ) || (parity && not(needs_something))\
)

goto(if(skipped, next, killer_loop))

; Start of kill enemies

#enemyInDir(x, y) adventure.isEnemy(adventure.playerPos() + vec({x},{y}))
; Check if an enemy is in the inputed direction

; Move in the enemies direction
#enemyDirection if({enemyInDir(0.0, 1.0)}, vec(0.0, 1.0),\
                if({enemyInDir(1.0, 0.0)}, vec(1.0, 0.0),\
                if({enemyInDir(0.0,-1.0)}, vec(0.0,-1.0),\
                if({enemyInDir(-1.0, 0.0)}, vec(-1.0, 0.0), \
                vec(0.0, 0.0)))))

; check if there are any enemies next to the player
#anyEnemies {enemyInDir(0.0, 1.0)} || {enemyInDir( 0.0,-1.0)} ||\
            {enemyInDir(1.0, 0.0)} || {enemyInDir(-1.0, 0.0)}

killer_wait:
adventure.wait()
; wait in our curent position

attack:
waitframe()
adventure.move({enemyDirection})
; move towards the enemy to attack it
; wait frame is needed here to handle invalid (0, 0) movements

killer_loop:
goto(if(\
  isopen("arcade") && {state.get} || impulse() == "key.{kill}",\
  if(\
    max({count.entities(Enemy)}, {count.entities(Elite)}) > 0,\
    if({anyEnemies}, attack, killer_wait),\
    if(\
      contains("ULRD", farm_dir) &&\
        (parity || needs_something && ({any_needs})),\
      next,\
      if(impulse() == "key.{kill}", end, main_loop)\
    )\
  ),\
  end\
))

main_loop:
; We check based on current difficulty plus 1,
; because it's more efficient that way.
difficulty = {cur_difficulty(1.0)}

; This line is responsible for maxDifficulty "creeping" upwards. When used
; properly, it will cause the AI to slowly make spirals that cover
; (nearly) the entire maze. However, extra lines can "trick" it and
; cause it to jump more difficulty levels than wanted.
;
; Note the check against current difficulty
; (which is current_difficulty + 1 here), which prevents this from
; triggering multiple times before the AI can travel to new rooms.
;
; We add 2 to maxDifficulty because adding 1 would cause us to overlap
; prior progress, and we'd immediately add another 1 anyway.
leon.adventure.maxDifficulty = if(\
  difficulty > leon.adventure.maxDifficulty \
    && {count.entities(Chest)} == 0,\
  leon.adventure.maxDifficulty + 2.0,\
  leon.adventure.maxDifficulty\
)

dir = if(\
  difficulty != 1.0 || dir == "",\
  {condRotateLeft({survivable(difficulty)}, {nextRoomLower})},\
  {rotateLeft(dir)}\
)

; Buy the first market item in our priority list that we don't have.
; We don't need to worry about checking for the market or
; available emeralds if we can't buy the item, we'll simply fail
; to purchase and continue on.

; This Lua macro constructs a data table that is used to iterate over
; the items efficiently. The encoding is length-2 size, length-3 position,
; and variable-length data. The size and position are for locating the
; *next* item.
#data_table {lua(\
  local market = {};\
  local items = table.pack(\
    "impaler", "hammer", "bootsPhasing",\
    "leechSword", "manaReaver", "thornsArmor", \
    "holyBomb", "eodArmor", "bookSpells"\
  );\
  for item = 1, #items do\
    market[item * 3] = items[item];\
  end\
  local pos = 0;\
  for item = 1, #items do\
    local offset = item * 3;\
    pos = pos + 5 + #market[offset];\
    local next_item = #(market[offset + 3] or "");\
    market[offset - 2] = string.format("%02d", 5 + next_item);\
    market[offset - 1] = string.format("%03d", pos)\
  end\
  return table.concat(market)\
)}

market_loop:
market_data = sub(\
  "{data_table}",\
  s2i(sub(market_data, 2, 3), 0),\
  s2i(sub(market_data, 0, 2), 12)\
)

; Loop until we find an unbought item. By the nature of hasItem, it will
; always return false for the empty string at the end of the loop, so we
; don't need an explicit end check.
goto(if(\
  isopen("arcade") && {state.get},\
  if(adventure.hasItem(sub(market_data, 5, 99)), market_loop, buy),\
  end\
))

buy:
adventure.buyMarketItem(sub(market_data, 5, 99))

; Turn our room direction into a target exit via lookup-table.
; "U    L    R    D    " = "{dir_spaced(4)}"
; "0918 0009 1809 0900"
:const string exit_idx "0918 0009 1809 0900"
{target.setPos(sub(exit_idx, index("{dir_spaced(4)}", dir, 0), 4) . ":")}
; 

executesync("{package}:analyze loot")
find_loot:
executesync("{package}:find path")
executesync("{package}:follow path")
{target.setPos(sub({target.getPositions}, 5, 9999))}

; keep looping until we no longer have any target positions
gotoif(find_loot, {state.get} && {target.getPositions} != "")

adventure.move({nextDir(dir)})

gotoif(start, isopen("arcade") && {state.get})

end:
; We reach the end 

; Our state is controlled by the top of the script, so we can
; only reach here if {state.get} == false or if arcade is closed.
; 
; If our state == false, we won't change anything,
; as we're already shut down but if arcade is closed,
; we must ensure that we shut down
{state.set(isopen("arcade") && {state.get})}
